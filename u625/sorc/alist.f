      SUBROUTINE ALIST(KFILDO,KFILCP,IP15,IDEQN,ID,IDPARS,TRESHL,JD,
     1                 KGP,MTRMS,ITAU,
     2                 ND2,ND4,ND13,NPRED,NSET,NSETS,IER)
C
C        JULY      2000   GLAHN   TDL    MOS-2000
C        AUG       2000   MCE     MODIFIED FORMAT STMNTS FOR IBM.
C
C        PURPOSE
C            TO FIND IN IDEQN(J, , ) ALL UNIQUE PREDICTORS AND TO
C            PLACE THEM IN ID(J, ) (J=1,4).  ADAPTED FRO U625 FROM
C            PLIST.
C   
C        DATA SET USE
C            KFILDO    - UNIT NUMBER OF OUTPUT (PRINT) FILE.  (OUTPUT)
C            KFILCP    - UNIT NUMBER FOR PREDICTOR CONSTANT FILE.
C                        (INPUT)
C            IP15      - THE PREDICTOR LIST IN SUMMARY FORM, NOT
C                        INCLUDING THE PARSED ID'S IN IDPARS( , ),
C                        BUT INCLUDING THE PLAIN LANGUAGE INFORMATION. 
C                        (OUTPUT)
C
C        VARIABLES
C              KFILDO = UNIT NUMBER OF OUTPUT (PRINT) FILE.  (INPUT)
C              KFILCP = UNIT NUMBER FOR PREDICTOR CONSTANT FILE.  THIS
C                       CONTAINS DEFAULT VALUES FOR CERTAIN CONSTANTS 
C                       FOR BASIC NMC PREDICTORS AND OTHER PREDICTORS 
C                       SANS THRESHOLDS, ETC.  THESE INCLUDE PLAIN
C                       LANGUAGE NAMES.  (INPUT)
C                IP15 = UNIT NUMBER FOR THE UNIQUE PREDICTOR LIST 
C                       IN SUMMARY FORM, NOT INCLUDING THE PARSED 
C                       ID'S IN IDPARS( , ), BUT INCLUDING THE
C                       PLAIN LANGUAGE INFORMATION.  (INPUT)
C        IDEQN(J,L,M) = THE 4-WORD ID (J=1,4) FOR EACH EQUATION
C                       (L=1,KGP), AND EACH PREDICTAND (M=1,NTAND).
C                       J=5,6,7 ARE NOT USED.  (INPUT)
C          ID(J,LIST) = THE LIST OF UNIQUE PREDICTORS (J=1,4)
C                       (LIST=1,NPRED).  (OUTPUT)
C         IDPARS(J,N) = THE PARSED, INDIVIDUAL COMPONENTS OF THE
C                       PREDICTOR ID'S CORRESPONDING TO ID( ,N) 
C                       (J=1,15), (N=1,NPRED).  (OUTPUT)
C                       J=1--CCC (CLASS OF VARIABLE),
C                       J=2--FFF (SUBCLASS OF VARIABLE),
C                       J=3--B (BINARY INDICATOR),
C                          0 = NOT BINARY,
C                          1 = CUMULATIVE FROM ABOVE, VALUES GE LOWER
C                              THRESHOLD TRESHL = 1,
C                          2 = CUMULATIVE FROM BELOW, VALUES LT UPPER 
C                              THRESHOLD TRESHU = 1.
C                          3 = DISCRETE BINARY.  VALUES GE LOWER 
C                              THRESHOLD AND LT UPPER THRESHOLD = 1.
C                          5 = GRID BINARY.  VALUES GE LOWER THRESHOLD
C                          ONLY THE VALUE OF 0, 1, OR 5 SHOULD BE USED
C                          FOR PREDICTORS;
C                          0, 1, 2, OR 3 CAN BE USED FOR PREDICTANDS,
C                          BUT THESE ARE NOT INVOLVED IN U700.
C                       J=4--DD (DATA SOURCE, MODEL NUMBER),
C                       J=5--V (VERTICAL APPLICATION),
C                       J=6--LBLBLBLB (BOTTOM OF LAYER, 0 IF ONLY 
C                            1 LAYER),
C                       J=7--LTLTLTLT (TOP OF LAYER),
C                       J=8--T (TRANSFORMATION),
C                       J=9--RR (RUN TIME OFFSET, ALWAYS + AND BACK 
C                            IN TIME),
C                       J=10--OT (TIME APPLICATION),
C                       J=11--OH (TIME PERIOD IN HOURS),
C                       J=12--TAU (PROJECTION IN HOURS),
C                       J=13--I (INTERPOLATION TYPE),
C                       J=14--S (SMOOTHING INDICATOR), AND
C                       J=15--G (GRID INDICATOR).
C           TRESHL(N) = THE LOWER BINARY THRESHOLD CORRESPONDING TO 
C                       IDPARS( ,N) (N=1,ND4).  (OUTPUT)
C             JD(J,N) = THE BASIC INTEGER VARIABLE ID'S (J=1,4)
C                       (N=1,ND4).  THIS IS THE SAME AS ID(J,N), EXCEPT
C                       THAT THE FOLLOWING PORTIONS ARE OMITTED:
C                       B = IDPARS(3, ),
C                       G = IDPARS(15, ), AND
C                       TRESHL( ).
C                       JD( , ) IS USED TO IDENTIFY WHICH CALCULATIONS
C                       CAN BE MADE DIRECTLY IN U700, WHICH IS ONLY 
C                       FORMING BINARIES.  THE "G" VARIABLE HAS NO 
C                       MEANING IN U700, IT BEING ONLY FOR POSSIBLE USE
C                       IN U201.
C                       IN ADDITION, THE TAU IS REMOVED FROM JD(3, )
C                       THE SAME AS FOR ID(3, ) FOR OBSERVATIONS.  (OUTPUT)
C                 KGP = THE NUMBER OF GROUPS.  (INPUT)
C            MTRMS(L) = THE NUMBER OF TERMS IN EACH EQUATION
C                       (L=1,KGP).  (INPUT)
C             ITAU(N) = THE NUMBER OF HOURS TO ADD TO NDATE TO GET 
C                       THE VARIABLE N (N=1,ND4).  THIS IS THE 
C                       "LOOKAHEAD" FEATURE.  USED ONLY FOR PRINTING.
C                       (OUTPUT)
C                 ND2 = MAXIMUM NUMBER OF TERMS IN ANY EQUATION.
C                       (INPUT)
C                 ND4 = MAXIMUM NUMBER OF DIFFERENT PREDICTORS IN RUN.
C                       THIS COULD BE AS LARGE AS ND1*ND2, BUT WOULD
C                       NORMALLY BE MUCH LESS, FOR LARGE ND1.  (INPUT)
C                ND13 = MAXIMUM NUMBER OF DIFFERENT EQUATIONS PER SET.
C                       THIS WOULD = ND1 FOR SINGLE STATION EQUATIONS,
C                       BUT MIGHT BE ON THE ORDER OF 30 FOR REGIONAL
C                       EQUATIONS.  (INPUT)
C               NPRED = THE NUMBER OF VALUES IN ID( , ).  (INPUT/OUTPUT)
C                NSET = THE NUMBER OF THE EQUATION FILE BEING DEALT
C                       WITH.  (INPUT)
C               NSETS = THE TOTAL NUMBER OF EQUATION FILES IN THE RUN.
C                       (INPUT)
C                 IER = STATUS RETURN.
C                         0 = GOOD RETURN
C                       131 = MORE UNIQUE PREDICTORS THAN ND4.
C           ISCALD(N) = SCALING FACTORS FOR THE PREDICTORS (N=1,ND4).
C                       THESE ARE NOT NEEDED IN U700, BUT ARE PRINTED
C                       IN ALIST FOR REFERENCE ONLY.  (INTERNAL)
C                       (AUTOMATIC)
C            PLAIN(N) = THE PLAIN LANGUAGE DESCRIPTION OF THE PREDICTORS
C                       (N=1,ND4).  THESE ARE PRINTED IN ALIST, BUT ARE
C                       NOT USED AGAIN.  (INTERNAL)  (CHARACTER*32)
C                       (AUTOMATIC)
C            SMULT(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C             SADD(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C           ORIGIN(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C             CINT(N) = WORK ARRAY FOR SETPLN.  (INTERNAL) (AUTOMATIC)
C 
C        NONSYSTEM SUBROUTINES USED 
C            NONE
C
      CHARACTER*12 UNITS(ND4)
      CHARACTER*32 PLAIN(ND4)
C        UNITS( ) AND PLAIN( ) ARE AUTOMATIC ARRAYS.
C
      DIMENSION MTRMS(ND13)
      DIMENSION IDEQN(7,ND13,ND2)
      DIMENSION ID(4,ND4),IDPARS(15,ND4),TRESHL(ND4),JD(4,ND4),ITAU(ND4)
      DIMENSION ISCALD(ND4),SMULT(ND4),SADD(ND4),ORIGIN(ND4),CINT(ND4)
C        ISCALD( ),SMULT( ),SADD( ),ORIGIN( ),AND CINT( ) ARE
C        AUTOMATIC ARRAYS USED PRIMARILY IN SETPLN.
C
D     CALL TIMPR(KFILDO,KFILDO,'START ALIST         ')
      IER=0
C
C        FILL ID( , ).
C
      DO 129 L=1,KGP
      DO 128 M=1,MTRMS(L)
C
      IF(NPRED.EQ.0)GO TO 111
C
C        LOOK FOR PREDICTOR IN ID( , ) LIST.
C 
      DO 110 LIST=1,NPRED
      DO 109 J=1,4
      IF(IDEQN(J,L,M).NE.ID(J,LIST))GO TO 110
 109  CONTINUE
C
C        THIS IS THE SAME PREDICTOR AS ID( ,LIST).
C
      GO TO 128
C
 110  CONTINUE
C
C        THIS IS A DIFFERENT PREDICTOR.  INSERT INTO ID( ,LIST).
C
 111  IF(NPRED.LT.ND4)GO TO 113
      WRITE(KFILDO,112)ND4,((ID(J,LIST),J=1,4),LIST=1,NPRED)
 112  FORMAT(/,' ****LIST OF UNIQUE PREDICTORS EXCEEDS ND4 =',I5,
     1         ' AT 112 IN ALIST.',/,(' ',I9.9,2I10,I11))
      IER=131
      GO TO 131
C
 113  NPRED=NPRED+1
C
      DO 117 J=1,4
      ID(J,NPRED)=IDEQN(J,L,M)
 117  CONTINUE
C
 128  CONTINUE
 129  CONTINUE
C
 131  CONTINUE
C
D     WRITE(KFILDO,135)((L,M,
D    1                 (IDEQN(J,L,M),J=1,4),M=1,MTRMS(L)),
D    2                 L=1,KGP)
D135  FORMAT(/' L,M,IDEQN IN ALIST'/
D    1      (' '2I4,I12.9,2I10,I11))
D     WRITE(KFILDO,140)((ID(J,LIST),J=1,4),LIST=1,NPRED)
D140  FORMAT(/' ID IN ALIST'/
D    1      (' 'I9.9,2I10,I11))
C
C        SUMMARIZE ONLY ON THE LAST SET OF EQUATIONS.
C
      IF(NSET.LT.NSETS)GO TO 170
C        PARSE VARIABLES INTO IDPARS( , ) AND PREPARE JD( , ).
C
      DO 145 LIST=1,NPRED
      CALL PRSID2(KFILDO,ID(1,LIST),IDPARS(1,LIST),TRESHL(LIST))
C
C        FOR ALL PROGRAMS EXCEPT U201, IDPARS(15, ) AND IT'S ROLE IN
C        ID(4) HAS NO MEANING.  THE INTERPOLATED INPUT FILE WILL NOT
C        INCLUDE IT.  THEREFORE, SET IT TO ZERO.
C
      IDPARS(15,LIST)=0
      ID(4,LIST)=(ID(4,LIST)/10)*10
C
C        PREPARE "BASIC" VARIABLE ID'S.  FOR PROGRAMS READING U201
C        OUTPUT (E.G., U600), JD( , ) = ID( , ) EXCEPT IN ID(1, )
C        THE BINARY INDICATOR IS OMITTED AND IN JD(4, )
C        THE THRESHOLDS AND IDPARS(15, ) ARE OMITTED.
C
      JD(1,LIST)=IDPARS(1,LIST)*1000000+
     1           IDPARS(2,LIST)*1000+
     2           IDPARS(4,LIST)
      JD(2,LIST)=ID(2,LIST)
      JD(3,LIST)=ID(3,LIST)
      JD(4,LIST)=IDPARS(13,LIST)*100+
     1           IDPARS(14,LIST)*10
 145  CONTINUE
C
C        PUT PLAIN LANGUAGE WITH THE PREDICTORS.
C
      CALL SETPLN(KFILDO,KFILCP,
     1            ID,IDPARS,JD,ISCALD,SMULT,SADD,
     2            ORIGIN,CINT,PLAIN,UNITS,ND4,NPRED,ISTOP,NOP)
C        RETURN FROM SETPLN IS ALWAYS 0.  RETAIN IER FROM ABOVE.
C
C        INITIALIZE ITAU( ).  IT IS ASSUMED THAT THE TAU
C        (IDPARS(12) FOR ANY OBSERVATION (CCC = 7XX) INDICATES
C        THAT VALUE IS TO BE ADDED TO IDATE( ) FOR THE DATE/TIME,
C        AND THE TAU IN THE ID IS ZERO.
C    
      DO 146 LIST=1,NPRED
C
      IF(IDPARS(1,LIST).GE.700.AND.IDPARS(1,LIST).LE.799)THEN
         ITAU(LIST)=IDPARS(12,LIST)
         IDPARS(12,LIST)=0
         ID(3,LIST)=ID(3,LIST)-ITAU(LIST)
         JD(3,LIST)=JD(3,LIST)-ITAU(LIST)
      ELSE
         ITAU(LIST)=0
      ENDIF
C
 146  CONTINUE
C
      IF(IP15.EQ.0)GO TO 170
C
C        WRITE THE PREDICTOR ID'S INCLUDING PLAIN LANGUAGE
C        TO UNIT NO. IP15.
C
      WRITE(IP15,160)NPRED
 160  FORMAT(/,' ',I4,' UNIQUE PREDICTORS AND INFORMATION FROM ',
     1         ' VARIABLE CONSTANT FILE')
      WRITE(IP15,161)(N,(ID(J,N),J=1,4),PLAIN(N),ITAU(N),ISCALD(N),
     1               N=1,NPRED)
 161  FORMAT('  NO.     ID(1)     ID(2)     ID(3)      ID(4)',
     1       '       PLAIN LANGUAGE ID          ITAU  ISCALD',
     2       ' (ISCALE NOT USED, FOR REFERENCE ONLY)',/, 
     3      (' ',I4,1X,I9.9,1X,I9.9,1X,I9.9,1X,I10.3,2X,A32,I3,I7))
C
 170  CONTINUE
C
D     CALL TIMPR(KFILDO,KFILDO,'STOP ALIST          ')
      RETURN
      END
